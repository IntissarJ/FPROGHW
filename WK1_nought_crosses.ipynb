{
 "cells": [
  {
   "cell_type": "code",
   "execution_count": 25,
   "metadata": {
    "dotnet_interactive": {
     "language": "fsharp"
    },
    "polyglot_notebook": {
     "kernelName": "fsharp"
    },
    "vscode": {
     "languageId": "polyglot-notebook"
    }
   },
   "outputs": [],
   "source": [
    "\n",
    "open System\n",
    "module TicTacToeDomain =\n",
    "\n",
    "    type HorizPosition = Left | HCenter | Right\n",
    "    type VertPosition = Top | VCenter | Bottom\n",
    "    type CellPosition = HorizPosition * VertPosition\n",
    "\n",
    "    type Player = PlayerO | PlayerX\n",
    "\n",
    "    type CellState =\n",
    "        | Played of Player\n",
    "        | Empty\n",
    "\n",
    "    type Cell = {\n",
    "        pos : CellPosition\n",
    "        state : CellState\n",
    "        }\n",
    "\n",
    "    type PlayerXPos = PlayerXPos of CellPosition\n",
    "    type PlayerOPos = PlayerOPos of CellPosition\n",
    "\n",
    "    type ValidMovesForPlayerX = PlayerXPos list\n",
    "    type ValidMovesForPlayerO = PlayerOPos list\n",
    "\n",
    "    type MoveResult =\n",
    "        | PlayerXToMove of ValidMovesForPlayerX\n",
    "        | PlayerOToMove of ValidMovesForPlayerO\n",
    "        | GameWon of Player\n",
    "        | GameTied\n",
    "\n",
    "    // the \"use-cases\"\n",
    "    type NewGame<'GameState> =\n",
    "        'GameState * MoveResult\n",
    "    type PlayerXMoves<'GameState> =\n",
    "        'GameState -> PlayerXPos -> 'GameState * MoveResult\n",
    "    type PlayerOMoves<'GameState> =\n",
    "        'GameState -> PlayerOPos -> 'GameState * MoveResult\n",
    "\n",
    "    // helper function\n",
    "    type GetCells<'GameState> =\n",
    "        'GameState -> Cell list\n",
    "\n",
    "    // the functions exported from the implementation\n",
    "    // for the UI to use.\n",
    "    type TicTacToeAPI<'GameState>  =\n",
    "        {\n",
    "        newGame : NewGame<'GameState>\n",
    "        playerXMoves : PlayerXMoves<'GameState>\n",
    "        playerOMoves : PlayerOMoves<'GameState>\n",
    "        getCells : GetCells<'GameState>\n",
    "        }\n",
    "\n",
    "module TicTacToeImplementation =\n",
    "    open TicTacToeDomain\n",
    "\n",
    "    /// private implementation of game state\n",
    "    type GameState = {\n",
    "        cells : Cell list\n",
    "        }\n",
    "\n",
    "    /// the list of all horizontal positions\n",
    "    let allHorizPositions = [Left; HCenter; Right]\n",
    "\n",
    "    /// the list of all horizontal positions\n",
    "    let allVertPositions = [Top; VCenter; Bottom]\n",
    "\n",
    "    /// A type to store the list of cell positions in a line\n",
    "    type Line = Line of CellPosition list\n",
    "\n",
    "    /// a list of the eight lines to check for 3 in a row\n",
    "    let linesToCheck =\n",
    "        let makeHLine v = Line [for h in allHorizPositions do yield (h,v)]\n",
    "        let hLines= [for v in allVertPositions do yield makeHLine v]\n",
    "\n",
    "        let makeVLine h = Line [for v in allVertPositions do yield (h,v)]\n",
    "        let vLines = [for h in allHorizPositions do yield makeVLine h]\n",
    "\n",
    "        let diagonalLine1 = Line [Left,Top; HCenter,VCenter; Right,Bottom]\n",
    "        let diagonalLine2 = Line [Left,Bottom; HCenter,VCenter; Right,Top]\n",
    "\n",
    "        // return all the lines to check\n",
    "        [\n",
    "        yield! hLines\n",
    "        yield! vLines\n",
    "        yield diagonalLine1\n",
    "        yield diagonalLine2\n",
    "        ]\n",
    "\n",
    "    /// get the cells from the gameState\n",
    "    let getCells gameState =\n",
    "        gameState.cells\n",
    "\n",
    "    /// get the cell corresponding to the cell position\n",
    "    let getCell gameState posToFind =\n",
    "        gameState.cells\n",
    "        |> List.find (fun cell -> cell.pos = posToFind)\n",
    "\n",
    "    /// update a particular cell in the GameState\n",
    "    /// and return a new GameState\n",
    "    let private updateCell newCell gameState =\n",
    "\n",
    "        // create a helper function\n",
    "        let substituteNewCell oldCell =\n",
    "            if oldCell.pos = newCell.pos then\n",
    "                newCell\n",
    "            else\n",
    "                oldCell\n",
    "\n",
    "        // get a copy of the cells, with the new cell swapped in\n",
    "        let newCells = gameState.cells |> List.map substituteNewCell\n",
    "\n",
    "        // return a new game state with the new cells\n",
    "        {gameState with cells = newCells }\n",
    "\n",
    "    /// Return true if the game was won by the specified player\n",
    "    let private isGameWonBy player gameState =\n",
    "\n",
    "        // helper to check if a cell was played by a particular player\n",
    "        let cellWasPlayedBy playerToCompare cell =\n",
    "            match cell.state with\n",
    "            | Played player -> player = playerToCompare\n",
    "            | Empty -> false\n",
    "\n",
    "        // helper to see if every cell in the Line has been played by the same player\n",
    "        let lineIsAllSamePlayer player (Line cellPosList) =\n",
    "            cellPosList\n",
    "            |> List.map (getCell gameState)\n",
    "            |> List.forall (cellWasPlayedBy player)\n",
    "\n",
    "        linesToCheck\n",
    "        |> List.exists (lineIsAllSamePlayer player)\n",
    "\n",
    "\n",
    "    /// Return true if all cells have been played\n",
    "    let private isGameTied gameState =\n",
    "        // helper to check if a cell was played by any player\n",
    "        let cellWasPlayed cell =\n",
    "            match cell.state with\n",
    "            | Played _ -> true\n",
    "            | Empty -> false\n",
    "\n",
    "        gameState.cells\n",
    "        |> List.forall cellWasPlayed\n",
    "\n",
    "    /// determine the remaining moves for a player\n",
    "    let private remainingMovesForPlayer playerMove gameState =\n",
    "\n",
    "        // helper to return Some if a cell is playable\n",
    "        let playableCell cell =\n",
    "            match cell.state with\n",
    "            | Played player -> None\n",
    "            | Empty -> Some (playerMove cell.pos)\n",
    "\n",
    "        gameState.cells\n",
    "        |> List.choose playableCell\n",
    "\n",
    "\n",
    "    /// create the state of a new game\n",
    "    let newGame =\n",
    "\n",
    "        // allPositions is the cross-product of the positions\n",
    "        let allPositions = [\n",
    "            for h in allHorizPositions do\n",
    "            for v in allVertPositions do\n",
    "                yield (h,v)\n",
    "            ]\n",
    "\n",
    "        // all cells are empty initially\n",
    "        let emptyCells =\n",
    "            allPositions\n",
    "            |> List.map (fun pos -> {pos = pos; state = Empty})\n",
    "\n",
    "        // create initial game state\n",
    "        let gameState = { cells=emptyCells }\n",
    "\n",
    "        // initial set of valid moves for player X is all positions\n",
    "        let validMoves =\n",
    "            allPositions\n",
    "            |> List.map PlayerXPos\n",
    "\n",
    "        // return new game\n",
    "        gameState, PlayerXToMove validMoves\n",
    "\n",
    "    // player X makes a move\n",
    "    let playerXMoves gameState (PlayerXPos cellPos) =\n",
    "        let newCell = {pos = cellPos; state = Played PlayerX}\n",
    "        let newGameState = gameState |> updateCell newCell\n",
    "\n",
    "        if newGameState |> isGameWonBy PlayerX then\n",
    "            // return the new state and the move result\n",
    "            newGameState, GameWon PlayerX\n",
    "        elif newGameState |> isGameTied then\n",
    "            // return the new state and the move result\n",
    "            newGameState, GameTied\n",
    "        else\n",
    "            let remainingMoves =\n",
    "                newGameState |> remainingMovesForPlayer PlayerOPos\n",
    "            newGameState, PlayerOToMove remainingMoves\n",
    "\n",
    "    // player O makes a move\n",
    "    let playerOMoves gameState (PlayerOPos cellPos) =\n",
    "        let newCell = {pos = cellPos; state = Played PlayerO}\n",
    "        let newGameState = gameState |> updateCell newCell\n",
    "\n",
    "        if newGameState |> isGameWonBy PlayerO then\n",
    "            // return the new state and the move result\n",
    "            newGameState, GameWon PlayerO\n",
    "        elif newGameState |> isGameTied then\n",
    "            // return the new state and the move result\n",
    "            newGameState, GameTied\n",
    "        else\n",
    "            let remainingMoves =\n",
    "                newGameState |> remainingMovesForPlayer PlayerXPos\n",
    "            newGameState, PlayerXToMove remainingMoves\n",
    "\n",
    "        // Exercise - refactor to remove the duplicate code from\n",
    "        // playerXMoves  and playerOMoves\n",
    "\n",
    "\n",
    "    /// export the API to the application\n",
    "    let api = {\n",
    "        newGame = newGame\n",
    "        playerOMoves = playerOMoves\n",
    "        playerXMoves = playerXMoves\n",
    "        getCells = getCells\n",
    "        }\n",
    "\n",
    "/// Console based user interface\n",
    "module ConsoleUi =\n",
    "    open TicTacToeDomain\n",
    "\n",
    "    /// Track the UI state\n",
    "    type UserAction<'a> =\n",
    "        | ContinuePlay of 'a\n",
    "        | ExitGame\n",
    "\n",
    "    /// Print each available move on the console\n",
    "    let displayAvailableMoves moves =\n",
    "        moves\n",
    "        |> List.iteri (fun i move ->\n",
    "            printfn \"%i) %A\" i move )\n",
    "\n",
    "    /// Get the move corresponding to the\n",
    "    /// index selected by the user\n",
    "    let getMove moveIndex moves =\n",
    "        if moveIndex < List.length moves then\n",
    "            let move = List.nth moves moveIndex\n",
    "            Some move\n",
    "        else\n",
    "            None\n",
    "\n",
    "    /// Given that the user has not quit, attempt to parse\n",
    "    /// the input text into a index and then find the move\n",
    "    /// corresponding to that index\n",
    "    let processMoveIndex (inputStr:string) gameState availableMoves makeMove processInputAgain =\n",
    "        match System.Int32.TryParse inputStr with\n",
    "        // TryParse will output a tuple (parsed?,int)\n",
    "        | true,inputIndex ->\n",
    "            // parsed ok, now try to find the corresponding move\n",
    "            match getMove inputIndex availableMoves with\n",
    "            | Some move ->\n",
    "                // corresponding move found, so make a move\n",
    "                let moveResult = makeMove gameState move\n",
    "                ContinuePlay moveResult // return it\n",
    "            | None ->\n",
    "                // no corresponding move found\n",
    "                printfn \"...No move found for inputIndex %i. Try again\" inputIndex\n",
    "                // try again\n",
    "                processInputAgain()\n",
    "        | false, _ ->\n",
    "            // int was not parsed\n",
    "            printfn \"...Please enter an int corresponding to a displayed move.\"\n",
    "            // try again\n",
    "            processInputAgain()\n",
    "\n",
    "    /// Ask the user for input. Process the string entered as\n",
    "    /// a move index or a \"quit\" command\n",
    "    let rec processInput gameState availableMoves makeMove =\n",
    "\n",
    "        // helper that calls this function again with exactly\n",
    "        // the same parameters\n",
    "        let processInputAgain() =\n",
    "            processInput gameState availableMoves makeMove\n",
    "\n",
    "        printfn \"Enter an int corresponding to a displayed move or q to quit:\"\n",
    "        let inputStr = Console.ReadLine()\n",
    "        if inputStr = \"q\" then\n",
    "            ExitGame\n",
    "        else\n",
    "            processMoveIndex inputStr gameState availableMoves makeMove processInputAgain\n",
    "\n",
    "    /// Display the cells on the console in a grid\n",
    "    let displayCells cells =\n",
    "        let cellToStr cell =\n",
    "            match cell.state with\n",
    "            | Empty -> \"-\"\n",
    "            | Played player ->\n",
    "                match player with\n",
    "                | PlayerO -> \"O\"\n",
    "                | PlayerX -> \"X\"\n",
    "\n",
    "        let printCells cells  =\n",
    "            cells\n",
    "            |> List.map cellToStr\n",
    "            |> List.reduce (fun s1 s2 -> s1 + \"|\" + s2)\n",
    "            |> printfn \"|%s|\"\n",
    "\n",
    "        let topCells =\n",
    "            cells |> List.filter (fun cell -> snd cell.pos = Top)\n",
    "        let centerCells =\n",
    "            cells |> List.filter (fun cell -> snd cell.pos = VCenter)\n",
    "        let bottomCells =\n",
    "            cells |> List.filter (fun cell -> snd cell.pos = Bottom)\n",
    "\n",
    "        printCells topCells\n",
    "        printCells centerCells\n",
    "        printCells bottomCells\n",
    "        printfn \"\"   // add some space\n",
    "\n",
    "    /// After each game is finished,\n",
    "    /// ask whether to play again.\n",
    "    let rec askToPlayAgain api  =\n",
    "        printfn \"Would you like to play again (y/n)?\"\n",
    "        match Console.ReadLine() with\n",
    "        | \"y\" ->\n",
    "            ContinuePlay api.newGame\n",
    "        | \"n\" ->\n",
    "            ExitGame\n",
    "        | _ -> askToPlayAgain api\n",
    "\n",
    "    /// The main game loop, repeated\n",
    "    /// for each user input\n",
    "    let rec gameLoop api userAction =\n",
    "        printfn \"\\n------------------------------\\n\"  // a separator between moves\n",
    "\n",
    "        match userAction with\n",
    "        | ExitGame ->\n",
    "            printfn \"Exiting game.\"\n",
    "        | ContinuePlay (state,moveResult) ->\n",
    "            // first, update the display\n",
    "            state |> api.getCells |> displayCells\n",
    "\n",
    "            // then handle each case of the result\n",
    "            match moveResult with\n",
    "            | GameTied ->\n",
    "                printfn \"GAME OVER - Tie\"\n",
    "                printfn \"\"\n",
    "                let nextUserAction = askToPlayAgain api\n",
    "                gameLoop api nextUserAction\n",
    "            | GameWon player ->\n",
    "                printfn \"GAME WON by %A\" player\n",
    "                printfn \"\"\n",
    "                let nextUserAction = askToPlayAgain api\n",
    "                gameLoop api nextUserAction\n",
    "            | PlayerOToMove availableMoves ->\n",
    "                printfn \"Player O to move\"\n",
    "                displayAvailableMoves availableMoves\n",
    "                let newResult = processInput state availableMoves api.playerOMoves\n",
    "                gameLoop api newResult\n",
    "            | PlayerXToMove availableMoves ->\n",
    "                printfn \"Player X to move\"\n",
    "                displayAvailableMoves availableMoves\n",
    "                let newResult = processInput state availableMoves api.playerXMoves\n",
    "                gameLoop api newResult\n",
    "\n",
    "    /// start the game with the given API\n",
    "    let startGame api =\n",
    "        let userAction = ContinuePlay api.newGame\n",
    "        gameLoop api userAction\n",
    "\n",
    "module Logger =\n",
    "    open TicTacToeDomain\n",
    "\n",
    "    let logXMove (PlayerXPos cellPos)=\n",
    "        printfn \"X played %A\" cellPos\n",
    "\n",
    "    let logOMove (PlayerOPos cellPos)=\n",
    "        printfn \"O played %A\" cellPos\n",
    "\n",
    "    /// inject logging into the API\n",
    "    let injectLogging api =\n",
    "\n",
    "        // make a logged version of the game function\n",
    "        let playerXMoves state move =\n",
    "            logXMove move\n",
    "            api.playerXMoves state move\n",
    "\n",
    "        // make a logged version of the game function\n",
    "        let playerOMoves state move =\n",
    "            logOMove move\n",
    "            api.playerOMoves state move\n",
    "\n",
    "        // create a new API with\n",
    "        // the move functions replaced\n",
    "        // with logged versions\n",
    "        { api with\n",
    "            playerXMoves = playerXMoves\n",
    "            playerOMoves = playerOMoves\n",
    "            }\n",
    "\n",
    "            \n",
    "module ConsoleApplication =\n",
    "\n",
    "    let startGame() =\n",
    "        let api = TicTacToeImplementation.api\n",
    "        ConsoleUi.startGame api\n",
    "\n"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 29,
   "metadata": {
    "dotnet_interactive": {
     "language": "fsharp"
    },
    "polyglot_notebook": {
     "kernelName": "fsharp"
    },
    "vscode": {
     "languageId": "polyglot-notebook"
    }
   },
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "Start programma\n",
      "Eind programma\n"
     ]
    }
   ],
   "source": [
    "printfn \"Start programma\"\n",
    "type Square = \n",
    "    | Empty\n",
    "    | Nought\n",
    "    | Cross\n",
    "\n",
    "let symbolOfSquare (s:Square): string = \n",
    "    match s with\n",
    "    | Empty -> \" \"\n",
    "    | Nought -> \"0\"\n",
    "    | Cross -> \"x\"\n",
    "\n",
    "type Row = Square * Square * Square\n",
    "\n",
    "type TripleIndex = \n",
    "    | First\n",
    "    | Second \n",
    "    | Third\n",
    "\n",
    "let squareOfRow (index : TripleIndex) (row: Row): Square = \n",
    "    match index, row with\n",
    "    | First, (s,_,_) -> s\n",
    "    | Second, (_,s,_) -> s\n",
    "    | Third, (_,_,s) -> s\n",
    "\n",
    "\n",
    "printfn \"Eind programma\" "
   ]
  }
 ],
 "metadata": {
  "language_info": {
   "name": "csharp"
  },
  "orig_nbformat": 4
 },
 "nbformat": 4,
 "nbformat_minor": 2
}
